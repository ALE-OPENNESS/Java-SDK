/** 
* Copyright 2025 ALE International
*
* Permission is hereby granted, free of charge, to any person obtaining a copy of this 
* software and associated documentation files (the "Software"), to deal in the Software 
* without restriction, including without limitation the rights to use, copy, modify, merge, 
* publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons 
* to whom the Software is furnished to do so, subject to the following conditions:
* 
* The above copyright notice and this permission notice shall be included in all copies or 
* substantial portions of the Software.
* 
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING 
* BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND 
* NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, 
* DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, 
* OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
**/
import static org.gradle.external.javadoc.JavadocMemberLevel.*

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.20.0'
}

group = 'io.github.ale-openness.o2g'

// Version rule
// O2G 2.7.4
//     ---
// SDK  3  4 1
// 3 = 2.7 incremented with each O2G version
// 4 = minor O2G version
// 1 = patch version SDK
version = '3.4.1'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

javadoc {
    source = sourceSets.main.allJava
    exclude("**/internal/**")
    exclude("com/ale/o2g/util/**")
    exclude("**/rsi/**")
    exclude("**/recording/**")
    exclude("com/ale/o2g/RsiService")
    exclude("com/ale/o2g/RecordingService")
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'o2g-sdk'
            version = project.version

            pom {
                name.set('o2g-sdk')
                description.set('A Java SDK for O2G')
                url.set('https://github.com/ALE-OPENNESS/Java-SDK')
                licenses {
                    license {
                        name.set('MIT License')
                        url.set('http://www.opensource.org/licenses/mit-license.php')
                    }
                }
                developers {
                    developer {
                        id.set('fredmab')
                        name.set('Frederic Mabileau')
                        email.set('frederic.mabileau@al-enterprise.com')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/ALE-OPENNESS/Java-SDK')
                    developerConnection.set('scm:git:git://github.com/ALE-OPENNESS/Java-SDK')
                    url.set('https://github.com/ALE-OPENNESS/Java-SDK')
                }
            }
        }
    }
    repositories {
        // Optional local staging directory
        maven {
            url = layout.buildDirectory.dir("staging-deploy")
        }
    }
}

signing {
    useGpgCmd() // ensures Gradle uses GPG for signing
    sign(publishing.publications.maven)
}

jreleaser {
    project {
        name = 'o2g-sdk'
        authors = ['Frederic Mabileau']
        license = 'MIT License'
        links {
            homepage = 'https://github.com/ALE-OPENNESS/Java-SDK'
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

